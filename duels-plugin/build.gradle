import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

clean.doFirst {
    delete "$rootDir/out/"
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    from(sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        filter(ReplaceTokens, tokens: [VERSION: project.version])
    }
}

dependencies {
    compileOnly 'org.jetbrains:annotations:24.0.1'
    compileOnly 'org.projectlombok:lombok:1.18.26'
    annotationProcessor 'org.projectlombok:lombok:1.18.26'
    compileOnly 'org.spigotmc:spigot-api:1.14.4-R0.1-SNAPSHOT'
    compileOnly 'com.mojang:authlib:1.5.21'
    compileOnly 'me.clip:placeholderapi:2.11.1'
    compileOnly 'com.github.sirblobman.api:core:2.7-SNAPSHOT'
    compileOnly 'com.github.sirblobman.combatlogx:api:11.2-SNAPSHOT'
    compileOnly ('net.essentialsx:EssentialsX:2.19.7') {
        transitive = false
    }
    compileOnly (name: 'MVdWPlaceholderAPI-3.1.1') {
        transitive = false
    }
    compileOnly name: 'Vault-1.6.7'
    compileOnly name: 'CombatTagPlus'
    compileOnly name: 'PvPManager-3.7.16'
    compileOnly name: 'Factions-1.6.9.5-U0.1.14'
    compileOnly name: 'MassiveCore'
    compileOnly name: 'Factions'
    compileOnly name: 'MyPet-2.3.4'
    compileOnly name: 'BountyHunters-2.2.6'
    compileOnly name: 'SimpleClans-2.14.4.1'
    compileOnly name: 'LeaderHeadsAPI'

    implementation project(':duels-api')
    implementation project(':duels-worldguard')
    implementation project(':duels-worldguard-v6')
    implementation project(':duels-worldguard-v7')
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.0'
}

shadowJar {
    getDestinationDirectory().set(file("$rootDir/out/"))

    final String archiveName = parent.name + '-' + project.version + '.jar'
    getArchiveFileName().set(archiveName)

    dependencies {
        include(project(':duels-api'))
        include(project(':duels-worldguard'))
        include(project(':duels-worldguard-v6'))
        include(project(':duels-worldguard-v7'))
        include(dependency('com.fasterxml.jackson.core:.*'))
    }

    final String group = project.group.toString() + "." + parent.name.toLowerCase() + ".shaded."
    relocate 'com.fasterxml.jackson.core', group + 'jackson-core'
}

// To build Duels plugin jar, run './gradlew clean build'.
build {
    dependsOn(shadowJar)
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group.toString()
            artifactId = project.name.toLowerCase()
            version = project.version
            from components.java
        }
    }
}
