import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        filter(ReplaceTokens, tokens: [VERSION: project.version])
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
    }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'

    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
    compileOnly 'org.spigotmc:spigot-api:1.17.1-R0.1-SNAPSHOT'

    compileOnly 'me.clip:placeholderapi:2.10.10'
    compileOnly('be.maximvdw:MVdWPlaceholderAPI:3.0.1-SNAPSHOT') { transitive = false }
    compileOnly 'com.SirBlobman.combatlogx:CombatLogX-API:10.0.0.0-SNAPSHOT', {
        exclude group: 'org.spigotmc'
        exclude group: 'org.bukkit'
    }
    compileOnly 'com.comphenix.protocol:ProtocolLib:4.7.0'

    implementation 'io.github.bananapuncher714:nbteditor:7.17.0', {
        exclude group: 'org.spigotmc'
    }
    implementation 'com.github.cryptomorin:XSeries:8.4.0'
    implementation 'com.google.code.gson:gson:2.8.8'

    compileOnly "com.github.MilkBowl:VaultAPI:1.7", {
        exclude group: 'org.spigotmc'
        exclude group: 'org.bukkit'
    }

    compileOnly name: 'CombatTagPlus'
    compileOnly name: 'PvPManager-3.7.16'
    compileOnly name: 'Factions-1.6.9.5-U0.1.14'
    compileOnly name: 'MassiveCore'
    compileOnly name: 'Factions'
    compileOnly name: 'MyPet-2.3.4'
    compileOnly name: 'BountyHunters-2.2.6'
    compileOnly name: 'LeaderHeadsAPI'

    implementation project(':duels-worldguard')
    implementation project(':duels-worldguard-v6')
    implementation project(':duels-worldguard-v7')
    implementation project(':duels-api')
}

shadowJar {
    archiveFileName.set(parent.name + '-' + project.version + '.jar')
    dependencies {
        include(dependency(':duels-worldguard'))
        include(dependency(':duels-worldguard-v6'))
        include(dependency(':duels-worldguard-v7'))
        include(dependency(':duels-api'))
        include(dependency('com.google.code.gson:.*'))
        include(dependency('com.github.cryptomorin:.*'))
        include(dependency('io.github.bananapuncher714:.*'))
    }
    def group = project.group.toString() + "." + parent.name.toLowerCase() + ".shaded."
    relocate 'io.github.bananapuncher714.nbteditor', group + 'nbteditor'
    relocate 'com.google.gson', group + 'gson'
    relocate "com.cryptomorin.xseries", group + 'xseries'
}

build.dependsOn(shadowJar)